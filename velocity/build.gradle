
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

configurations {
    shade
    compileOnly.extendsFrom shade
}

repositories {

    maven {
        url 'https://nexus.velocitypowered.com/repository/maven-public/'
    }

}

dependencies {

    compileOnly("com.velocitypowered:velocity-api:1.1.7")

    shade(project(':api'))
    shade(project(':common'))

}

processResources {

    Map<String, ?> ex = new HashMap<>()
    ex.put("version", project.ext.fullVersion)
    ex.put("modid", project.ext.modid)
    ex.put("modname", project.ext.modname)
    ex.put("modclass", project.ext.modclass)

    filesMatching("velocity-plugin.json") {
        expand(ex)
    }
}

shadowJar {

    archiveBaseName.set("${project.ext.archiveName}-velocity")
    configurations = [project.configurations.shade]

    dependencies {
        include(project(':api'))
        include(project(':common'))
    }
}

tasks.assemble.dependsOn tasks.shadowJar

artifacts {
    archives jar
    shadow sourcesJar
}

publishing {

    publications {
        mavenJava(MavenPublication) {

            version = project.ext.publicationVersion

            artifact(jar)
            artifact(sourcesJar)
        }
    }

    repositories {
        maven {
            if (project.hasProperty("pubUrl")) {
                url = project.pubUrl as String
                credentials {
                    username project.pubUser as String
                    password project.pubPass as String
                }
            }
        }
    }
}